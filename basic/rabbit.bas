1000	PRINT"DO YOU WANT INSTRUCTIONS :";
1010	INPUT A$
1020	IF MID$(A$,1,1)="N" THEN 1220
1030	IF MID$(A$,1,1)="n" THEN 1220
1040	PRINT
1050	PRINT
1060	PRINT"    This is the game of RABBIT.  The object of RABBIT is"
1070	PRINT"to come within a certian distance of the computer controlled"
1080	PRINT"rabbit, which also moves.  "
1090	PRINT"    The way you do this is to hop around (the rabbit hops too) "
1100	PRINT"such that your path crosses the rabbit's path at the same time"
1110	PRINT"Or in other words you come close enough to grab the little "
1120	PRINT"booger.  "
1130	PRINT"    Oh by the way your arms are about 20 units long.  The number"
1140	PRINT"'CLOSEST APPROACH' is the closest distance you and your bouncy"
1150	PRINT"friend came to each other during the last hop.  The 'DISTANCE"
1160	PRINT"TO RABBIT' is the distance you are from the rabbit now.  The"
1170	PRINT"rabbits 'DIRECTION' is the direction he is just about to hop,"
1180	PRINT"so keep that in mind when jumping.  The computer will prompt"
1190	PRINT"you for the direction you want to jump next.  Remember "
1200	PRINT"you and the rabbit don't necessarily jump at the same velocity."
1210	PRINT"   GOOD LUCK"
1220	PRINT
1230	PRINT"                   TYPE 'QUIT' TO STOP."
1240	PRINT
1250	RANDOMIZE
1260	REM -- ( `.T` IS THE SQUARE OF THE CAPTURE DISTANCE ) --
1270	LET T = 400
1280	REM
1290	REM -- INITIALIZE VELOCITIES AND POSITIONS --
1300	REM
1310	LET V1 = INT(RND(0)*10+0.5)*10+50
1320	LET V2 = (INT(RND(0)*2+0.5)+1)*V1
1330	LET X1 = (INT(RND(0)*400)+100)*SGN(RND(0)-0.5)
1340	LET Y1 = (INT(RND(0)*400)+100)*SGN(RND(0)-0.5)
1350	IF Y1 = 0  THEN 1330
1360	IF X1 = 0 THEN 1330
1370	LET X2 = 0
1380	LET Y2 = 0
1390	PRINT"SPEEDS (UNITS/HOP):"
1400	PRINT"RABBIT -";V1,"YOU -";V2
1410	PRINT
1420	PRINT
1430	PRINT
1440	LET C = (X2-X1)^2+(Y2-Y1)^2
1450	LET P1 = 3.14159/180
1460	LET H1 = 1
1470	REM
1480	REM -- PRINT OUT --
1490	REM
1500	LET D1 = INT(RND(0)*359)
1510	PRINT"HOP#: ";
1520	LET Z = H
1530	GOSUB 2330
1540	PRINT"    DISTANCE TO RABBIT: ";
1550	LET Z = SQR((X2-X1)^2+(Y2-Y1)^2)
1560	GOSUB 2330
1570	PRINT"    CLOSEST APPROACH: ";
1580	LET Z = SQR(C)
1590	GOSUB 2330
1600	PRINT
1610	PRINT"RABBIT   ---    POSITION:  (";
1620	LET Z = X1
1630	GOSUB 2280
1640	PRINT",";
1650	LET Z = Y1
1660	GOSUB 2280
1670	PRINT")     AND DIRECTION:  ";
1680	LET Z = D1
1690	GOSUB 2330
1700	PRINT
1710	PRINT"YOU      ---    POSITION:  (";
1720	LET Z = X2
1730	GOSUB 2280
1740	PRINT",";
1750	LET Z = Y2
1760	GOSUB 2280
1770	PRINT")     AND DIRECTION: ";
1780	GOTO 1800
1790	PRINT "DIRECTION TO HOP (0-359 DEGREES): ";
1800	INPUT A$
1810	DIM A(15)
1820	LET A(1)=A(2)=A(3)=A(4)=A(5)=D2=0
1830	CHANGE A$ TO A
1840	FOR I = 1 TO 15
1850	    IF A(I) = 0 THEN 1910
1859	    IF A(I) =113 THEN 2240
1860	    IF A(I) = 81 THEN 2240
1870	    IF A(I)-48 < 0 THEN 1790
1880	    IF A(I)-48 > 9 THEN 1790
1890	NEXT I
1900	GO TO 1790
1910	FOR J = 1 TO I-1
1920	    D2=D2+(A(I-J)-48)*10^(J-1)
1930	NEXT J
1940	IF D2 < 0 THEN 1790
1950	IF D2 >= 360 THEN 1790
1960	PRINT
1970	PRINT
1980	REM
1990	REM -- COMPUTE PATHS AND SEE IF THEY INTERSECT --
2000	REM
2010	LET X3 = V1*COS(D1*P1)/100
2020	LET Y3 = V1*SIN(D1*P1)/100
2030	LET X4 = V2*COS(D2*P1)/100
2040	LET Y4 = V2*SIN(D2*P1)/100
2050	LET C = (X2-X1)^2+(Y2-Y1)^2
2060	FOR I = 1 TO 100
2070	    LET X1 = X1+X3
2080	    LET Y1 = Y1+Y3
2090	    LET X2 = X2+X4
2100	    LET Y2 = Y2+Y4
2110	    IF C < (X2-X1)^2+(Y2-Y1)^2 THEN 2130
2120	    C = (X2-X1)^2+(Y2-Y1)^2
2130	NEXT I
2140	LET H = H+1
2150	IF C > T THEN 1500
2160	PRINT
2170	PRINT
2180	PRINT"**********"
2190	PRINT"* GOT YA *"
2200	PRINT"**********"
2210	PRINT
2220	PRINT
2230	PRINT
2240	STOP
2250	REM
2260	REM -- CONVERT NUMBERS TO STRINGS FOR CLEANER OUTPUT --
2270	REM
2280	IF Z < 0 THEN 2310
2290	PRINT "+";
2300	GOTO 2330
2310	PRINT"-";
2320	LET Z = -Z
2330	LET Z = INT(Z+0.5)
2340	LET S$ = "0123456789"
2350	FOR I = 1 TO 4
2360	    LET W = INT(Z/10^(4-I))
2370	    PRINT MID$(S$,W+1,1);
2380	    LET Z = Z-W*10^(4-I)
2390	NEXT I
2400	RETURN
2410	END
